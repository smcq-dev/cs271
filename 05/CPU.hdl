// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/5/CPU.hdl
/**
 * The Hack Central Processing unit (CPU).
 * Parses the binary code in the instruction input and executes it according to the
 * Hack machine language specification. In the case of a C-instruction, computes the
 * function specified by the instruction. If the instruction specifies to read a memory
 * value, the inM input is expected to contain this value. If the instruction specifies
 * to write a value to the memory, sets the outM output to this value, sets the addressM
 * output to the target address, and asserts the writeM output (when writeM = 0, any
 * value may appear in outM).
 * If the reset input is 0, computes the address of the next instruction and sets the
 * pc output to that value. If the reset input is 1, sets pc to 0.
 * Note: The outM and writeM outputs are combinational: they are affected by the
 * instruction's execution during the current cycle. The addressM and pc outputs are
 * clocked: although they are affected by the instruction's execution, they commit to
 * their new values only in the next cycle.
 */
CHIP CPU {

    IN  inM[16],         // M value input  (M = contents of RAM[A])
        instruction[16], // Instruction for execution
        reset;           // Signals whether to re-start the current
                         // program (reset==1) or continue executing
                         // the current program (reset==0).

    OUT outM[16],        // M value output
        writeM,          // Write to M? 
        addressM[15],    // Address in data memory (of M)
        pc[15];          // address of next instruction

    PARTS:
	//// Replace this comment with your code.

    //Is it a c instruction?
    Or(a= instruction[15], b= false, out= isCinstruction);

    // select alu y input
    Mux16(a= aout, b= inM, sel= instruction[12], out= aluY);

    // alu
    ALU(x= dout, y= aluY, zx= instruction[11], nx= instruction[10]
    , zy= instruction[9], ny= instruction[8], f= instruction[7], 
    no= instruction[6], out= aluOut, out=outM, zr= zr, ng= ng);

    // a register
    Mux16(a[0..14]= instruction[0..14], a[15]=false, b= aluOut, sel= isCinstruction, out= aIn);
    Not(in= isCinstruction, out= isAinstruction);
    And(a= isCinstruction, b= instruction[5], out= isCwriteA);
    Or(a= isCwriteA, b= isAinstruction, out= loadA);
    ARegister(in= aIn, load= loadA, out= aout, out[0..14]= addressM);

    // d register
    And(a=isCinstruction, b=instruction[4], out=loadD);
    DRegister(in=aluOut, load=loadD, out=dout);


    // writeM
    And(a= isCinstruction, b= instruction[3], out= writeM);


    // jump conditions
    Not(in= ng, out= notng);
    Not(in= zr, out= notzr);
    And(a= notzr, b= notng, out= pos);


    // check for conditions
    And(a= instruction[0], b= pos, out= JGT);
    And(a= instruction[1], b= zr, out= JEQ);
    And(a= instruction[2], b= ng, out= JLT);


    // combine all
    Or(a= JGT, b= JEQ, out= temp);
    Or(a= temp, b= JLT, out= jump);

    
    // jump only if c
    And(a= isCinstruction, b= jump, out= pcload);


    // pc
    PC(in= aout, load= pcload, inc= true, reset= reset, out[0..14]= pc);


   
}